group "me.minidigger.voxelgameslib"
version "1.0-SNAPSHOT"

defaultTasks "build"

buildscript {
    repositories {
        maven {
            url "https://jcenter.bintray.com/"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.+'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.+'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.+'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.+'
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "nebula-aggregate-javadocs"
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
    maven {
        url "http://atlas.minidigger.me:4444/nexus/content/groups/public"
    }
}

shadowJar {
    archiveName = "externaltools-${version}.jar"
    mergeServiceFiles()
    dependencies {
        exclude(dependency('org.projectlombok:lombok:.*'))
    }
    relocate 'com', 'me.minidigger.voxelgameslib.libs.com'
    relocate 'org', 'me.minidigger.voxelgameslib.libs.org'
    relocate 'jskill', 'me.minidigger.voxelgameslib.libs.jskill'
    relocate 'lombok', 'me.minidigger.voxelgameslib.libs.lombok'
    relocate 'javax', 'me.minidigger.voxelgameslib.libs.javax'
    relocate 'javassist', 'me.minidigger.voxelgameslib.libs.javassist'
    relocate 'antlr', 'me.minidigger.voxelgameslib.libs.antlr'
    relocate 'net', 'me.minidigger.voxelgameslib.libs.net'
}

dependencies {
    compile project(":jskill")
    compile project(":bungeecord-chat")
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    compileJava.options.encoding = "UTF-8"

    repositories {
        mavenCentral()
        maven {
            url "https://jcenter.bintray.com/"
        }
    }

    dependencies {
        testCompile "junit:junit:4.12"
        compile "com.google.code.gson:gson:2.8.0"
        compile "org.projectlombok:lombok:1.16.14"
        compile "com.google.inject:guice:4.1.0"
        compile "org.hibernate:hibernate-core:5.2.8.Final"
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    test {
        reports.html.enabled = false
        filter {
            includeTestsMatching "*Test*"
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true //TODO fix javadoc and sources
}

nexus {
    sign = true
    repositoryUrl = 'http://atlas.minidigger.me:4444/repository/maven-public/'
    snapshotRepositoryUrl = 'http://atlas.minidigger.me:4444/repository/maven-snapshots/'
}

artifacts {
    archives shadowJar
}

task testReport(type: TestReport) {
    println "test report exported to $buildDir/reports/allTests"
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects*.test
}

println """
*********************************************************
You are compiling VoxelGamesLib
if you encounter any issues, create a issue on github
https://github.com/VoxelGamesLib/VoxelGamesLib/issues
*********************************************************
"""
